apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.huangyu.easyframework"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {}

        release {
            storeFile file(rootProject.ext.jksPath)
            storePassword rootProject.ext.storePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.all {
                def name = rootProject.ext.projectName
                def channel = ''
                def releaseTime = new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+8"))
                outputFileName = "${name}_v${defaultConfig.versionName}.${defaultConfig.versionCode}.${releaseTime}_release.pro_${channel}.apk"
            }
        }
        if (variant.buildType.name == 'debug') {
            variant.outputs.all {
                def name = rootProject.ext.projectName
                def channel = ''
                def releaseTime = new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+8"))
                outputFileName = "${name}_v${defaultConfig.versionName}.${defaultConfig.versionCode}.${releaseTime}_debug_${channel}.apk"
            }
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':library')

    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknife"

    compile "com.github.bumptech.glide:glide:$rootProject.ext.glide"
    compile "com.github.chrisbanes:PhotoView:$rootProject.ext.photoview"
}
